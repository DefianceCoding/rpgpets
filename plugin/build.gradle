configurations {
    server {
        transitive = false
    }
    serverPlugin {
        transitive = false
    }
}

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.0'
    }
}

apply plugin: 'com.github.johnrengelman.shadow'

shadowJar {
    baseName = 'RPGPets'
    relocate 'org.bstats', 'me.yamakaja.rpgpets.lib.bstats'
}

dependencies {
    compileOnly "org.spigotmc:spigot-api:$spigotApiVersion"

    compile project(":api")
    compile project(":v1_11_R1")
    compile project(":v1_12_R1")

    compileOnly "com.comphenix.protocol:ProtocolLib:$protocolLibVersion"
    compileOnly "com.google.code.gson:gson:2.8.0"

    compile 'com.getsentry.raven:raven:8.0.3'
    compile "org.bstats:bstats-bukkit:1.1"

    server "org.spigotmc:spigot:$spigotServerVersion"

    serverPlugin "com.comphenix.protocol:ProtocolLib:$protocolLibVersion"
    serverPlugin "com.sk89q.worldguard:worldguard:$worldGuardVersion"
    serverPlugin "com.sk89q.worldedit:worldedit:$worldEditVersion"
    serverPlugin "us.forseth11.feudal:feudal:$feudalVersion"
    serverPlugin "net.milkbowl.vault:Vault:$vaultVersion"
    serverPlugin "net.drtshock.essentialsx:core:$essentialsVersion"
    serverPlugin "net.drtshock.essentialsx:chat:$essentialsVersion"
    serverPlugin "com.palmergames.towny:towny:$townyVersion"
}

jar {
    from {
        configurations.compile.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}

jar.baseName = "RPGPets"

processResources {
    filesMatching("**/plugin.yml") {
        filter {
            it.replace('@version@', project.getVersion())
        }
    }
}

task installPlugins(type: Copy) {
    print("Installing required plugins ... ")
    from(configurations.serverPlugin)
    into "$rootDir/testserver/plugins"
    rename("([^-]*).*\\.jar", '$1.jar')
    println("Done.")
}

task installServer(type: Copy) {
    print("Installing spigot server ... ")
    from(configurations.server)
    into "$rootDir/testserver/"
    rename { 'spigot.jar' }
    println("Done.")
    installPlugins.execute()
}

task install(type: Copy) {
    from("$rootDir/plugin/build/libs/") {
        include "**/RPGPets-*.jar"
    }
    rename("([^-]*).*\\.jar", '$1.jar')
    into "$rootDir/testserver/plugins/"
}
